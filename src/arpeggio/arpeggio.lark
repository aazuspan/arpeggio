song: _EOL* config_dict tracks

config_dict: config*
config: "@" key value? _EOL
key: WORD
value: NOTE_NAME | WORD | SIGNED_NUMBER
NOTE_NAME: LETTER+ (ACCIDENTAL | LETTER | "_")*
ACCIDENTAL: "#" | "b"

tracks: track* -> as_list
track: _track_start config_dict lines _track_end
_track_start: "track" _EOL+
_track_end: "end" _EOL*
lines: line*    -> as_list
line: symbols repeat? _EOL+
symbols: (interval | continue | rest)+ -> as_list
interval: (INT octave?)
!octave: "+" | "-" | "*" | "_"
repeat: "[x" INT "]"
continue: "."
rest: "&"

COMMENT: "~" /[^\n]*/ _EOL
%import common.INT
%import common.SIGNED_NUMBER
%import common.WS_INLINE
%import common.NEWLINE -> _EOL
%import common.LETTER
%import common.WORD
%ignore COMMENT
%ignore WS_INLINE