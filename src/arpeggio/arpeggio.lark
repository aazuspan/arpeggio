song: config_dict tracks

config_dict: config*
config: "@" key value?
key: WORD
value: NOTE_NAME | WORD | SIGNED_NUMBER
NOTE_NAME: LETTER+ (ACCIDENTAL | LETTER | "_")*
ACCIDENTAL: "#" | "b"

tracks: track* -> as_list
track: "track" config_dict lines "end"
lines: line*    -> as_list
line: "|" symbols repeat?
symbols: (interval | continue | rest)+ -> as_list
interval: (INT OCTAVE?)
repeat: "[x" INT "]"
continue: "."
rest: "&"

OCTAVE: "+" | "-" | "*" | "_"
COMMENT: "~" /[^\n]*/
%import common.INT
%import common.SIGNED_NUMBER
%import common.WS
%import common.LETTER
%import common.WORD
%ignore COMMENT
%ignore WS